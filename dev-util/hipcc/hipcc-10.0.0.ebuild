# Copyright 2026 Gentoo Authors
# Distributed under the terms of the GNU General Public License v2

EAPI=8

LLVM_COMPAT=( 23 )
inherit cmake llvm-r2

DESCRIPTION="Radeon Open Compute hipcc"
HOMEPAGE="https://github.com/ROCm/llvm-project/tree/amd-staging/amd/hipcc"

MY_P=llvm-project-rocm-${PV}
MY_TAG=therock-$(ver_cut 1-2)
components=( "amd/hipcc" )
SRC_URI="https://github.com/ROCm/llvm-project/archive/${MY_TAG}.tar.gz -> ${MY_P}.tar.gz"
S="${WORKDIR}/llvm-project-${MY_TAG}/${components[0]}"

LICENSE="Apache-2.0 MIT"
SLOT="0/$(ver_cut 1-2)"
KEYWORDS="~amd64"
IUSE="debug"

DEPEND="
	$(llvm_gen_dep "
		llvm-runtimes/compiler-rt:\${LLVM_SLOT}=
		llvm-core/llvm:\${LLVM_SLOT}=
		llvm-core/clang:\${LLVM_SLOT}=
	")
"
RDEPEND="${DEPEND}"

PATCHES=(
	"${FILESDIR}"/${PN}-10.0.0-old-offload-driver.patch
)

src_unpack() {
	local archive="${MY_P}.tar.gz"
	ebegin "Unpacking from ${archive}"
	tar -x -z -o \
		-f "${DISTDIR}/${archive}" \
		"${components[@]/#/llvm-project-${MY_TAG}/}" || die
	eend ${?}
}

src_prepare() {
	cmake_src_prepare

	# clang lives in /usr/lib/llvm/<slot>/bin instead of ${ROCM_PATH}/lib/llvm/bin
	sed -e "s:/ \"llvm\" / \"bin\":/ \"llvm\" / \"${LLVM_SLOT}\" / \"bin\":" \
		-e "s:/opt/rocm:/usr:g" \
		-i src/hipBin_base.h || die
	sed -e "s:hipClangPath /= \"llvm\";:hipClangPath /= \"llvm/${LLVM_SLOT}\";:" \
		-e "s:/opt/rocm:/usr:g" \
		-i src/hipBin_amd.h || die

	sed -e "s:amdgcn/bitcode:lib/amdgcn/bitcode:g" \
		-i src/hipBin_amd.h || die
}

src_install() {
	cmake_src_install
	# remove bat files...
	rm -rf "${ED}/usr/hip" || die
}
